#!/usr/bin/env zsh

prompt_nicoulaj_help () {
  cat <<'EOF'
This prompt shows 'user@hostname' only if connected over SSH. The path is shown relatively to the current VCS
repository root if any, and cropped to a maximum length at any times. It is colored to indicate the last
command exit status.

The right prompt shows info about the current VCS repository and status.

This prompt is configurable. You can invoke it thus:

  prompt nicoulaj [<path_length> [<color_success> [<color_failure> [<color_vcs_info> [<color_vcs_changes>]]]]]

where the settings are:
 * path_length: maximum path length in the left prompt (default: 40)
 * color_success: color of the left prompt when the previous command exit code is zero (default: green)
 * color_failure: color of the left prompt when the previous command exit code is non-zero (default: red)
 * color_vcs_info: color of the right prompt showing VCS info (default: grey)
 * color_vcs_changes: color of the right prompt showing VCS changes (default: yellow)
EOF
}

function prompt_nicoulaj_setup() {
  setopt localoptions noxtrace noksharrays
  prompt_opts=(cr subst percent)

  # Prompt settings.
  prompt_nicoulaj_path_length=${1:-40}
  prompt_nicoulaj_color_success=${2:-'%F{green}'}
  prompt_nicoulaj_color_failure=${3:-'%F{red}'}
  prompt_nicoulaj_color_faint=${4:-'%F{grey}'}
  prompt_nicoulaj_color_changes=${5:-'%F{yellow}'}
  prompt_nicoulaj_char=${6:-'>'}

  # Load required modules.
  autoload -Uz add-zsh-hook vcs_info && {

    # Add hook for calling vcs_info before each command.
    add-zsh-hook precmd vcs_info

    # vcs_info setup.
    zstyle ':vcs_info:*' enable hg bzr svn git
    zstyle ':vcs_info:*' check-for-changes true # Can be slow on big repos.
    zstyle ':vcs_info:*' unstagedstr " *"
    zstyle ':vcs_info:*' max-exports 3
    zstyle ':vcs_info:*' actionformats "%S" "%r/%b" " (%a)%u"
    zstyle ':vcs_info:*' formats "%S" "%r/%b" "%u"
    zstyle ':vcs_info:*' nvcsformats "%~"
    zstyle ':vcs_info:*:*:'$MAIN_USER formats "%S"

    # Prompts definition.
    PROMPT="%(0?.%{${prompt_nicoulaj_color_success}%}.%{${prompt_nicoulaj_color_failure}%})${SSH_TTY:+[%n@%m]}%{$FX[bold]%}%${prompt_nicoulaj_path_length}<..<"'${vcs_info_msg_0_%%.}'"%<<%(!.${prompt_nicoulaj_char}${prompt_nicoulaj_char}${prompt_nicoulaj_char}.${prompt_nicoulaj_char})%{$FX[no-bold]%}%{$FX[reset]%} "
    PROMPT2='%{${prompt_nicoulaj_color_faint}%}%_%{$FX[reset]%}%{${prompt_nicoulaj_color_success}%}${prompt_nicoulaj_char}%{$FX[reset]%} '
    PROMPT3='? %{${prompt_nicoulaj_color_success}%}${prompt_nicoulaj_char}%{$FX[reset]%} '
    PROMPT4='%{${prompt_nicoulaj_color_faint}%}%N:%i%{$FX[reset]%}%{${prompt_nicoulaj_color_success}%}${prompt_nicoulaj_char}%{$FX[reset]%} '
    RPROMPT="%{${prompt_nicoulaj_color_faint}%}"'$vcs_info_msg_1_'"%{$FX[reset]%}%{${prompt_nicoulaj_color_changes}%}"'$vcs_info_msg_2_'"%{$FX[reset]%}"
    SPROMPT='Correct ${prompt_nicoulaj_color_failure}$FX[bold]%R$FX[reset] to ${prompt_nicoulaj_color_success}$FX[bold]%r$FX[reset] ? ${prompt_nicoulaj_color_faint}[nyae]$FX[reset] '
  }
}

prompt_nicoulaj_setup "$@"

# vim: ft=zsh sw=2 ts=2 et
